plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'onethreeseven'
version '0.0.1'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
}

sourceCompatibility = 1.9

repositories {
    mavenLocal()
    mavenCentral()
    maven{url 'https://dl.bintray.com/lukehb/137-datastructures'} //some datastructures we use
    maven{url 'https://dl.bintray.com/lukehb/137-geo'} //mapping projections mostly
    maven{url 'https://dl.bintray.com/lukehb/137-common'} //some custom utilities we use
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    //data-structures
    compile (group: 'onethreeseven', name: 'datastructures', version: '0.0.4-SNAPSHOT') {force = true}
    compile (group: 'onethreeseven', name: 'common', version: '0.0.5-SNAPSHOT') {force = true}
    compile (group: 'onethreeseven', name: 'collections', version: '0.0.2')
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'

    //for loading pbf files from OSM
    compile group: 'org.openstreetmap.osmosis', name: 'osmosis-osm-binary', version: '0.44.1'
    compile group: 'org.openstreetmap.osmosis', name: 'osmosis-areafilter', version: '0.44.1'
    compile group: 'org.openstreetmap.osmosis', name: 'osmosis-xml', version: '0.44.1'
    compile group: 'org.openstreetmap.osmosis', name: 'osmosis-pbf2', version: '0.44.1'
    compile group: 'org.openstreetmap.osmosis', name: 'osmosis-pbf', version: '0.44.1'
    //map-matching
    compile(group: 'com.graphhopper', name: 'graphhopper-map-matching-core', version: '0.9.0') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    //for geometry
    compile group: 'com.vividsolutions', name: 'jts', version: '1.13'
    //for four-square api
    compile group: 'me.atlis', name: 'foursquare-api', version: '1.0.6'
}


ext.moduleName = 'onethreeseven.stosem'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
                'Automatic-Module-Name': moduleName,
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'))
    }
}

jar.dependsOn(test)

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc{
    options.addStringOption('-module-path', classpath.asPath)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version version
            Artifact sourcesJar
            Artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true //If version should be auto published after an upload
    pkg {
        repo = '137-stosem'
        name = '137-stosem'
        licenses = ['MIT']
        desc = 'Semantic trajectories generation and mining from opensteetmap places and human trajectories.'
        websiteUrl = 'https://github.com/lukehb/STOSEM'
        issueTrackerUrl = 'https://github.com/lukehb/STOSEM/issues'
        vcsUrl = 'https://github.com/lukehb/STOSEM.git'
        labels = ['java', 'data-mining', 'semantic', 'trajectory', 'openstreetmap']
        publicDownloadNumbers = true
        githubRepo = 'lukehb/STOSEM'
        githubReleaseNotesFile = 'README.md'
    }
}
